&Вместо("АвтоПодборАдреса")
Процедура dd_АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	Если СтрДлина(Текст) < 3 ИЛИ Прав(Текст, 1) = " " Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиПодсистемы = dd_ОбщийМодульПовторноеИспользование.ПолучитьНастройкиПодсистемы();
	
	Если НЕ НастройкиПодсистемы.ИспользованиеСервиса Тогда
		ПродолжитьВызов(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
		Возврат;
	КонецЕсли;
	
	СоответствиеОтвет = dd_ЗапросыКСервису.ПолучитьСоответствиеПодсказок(Текст, "Адрес"); 
	
	Если СоответствиеОтвет.Количество() = 0  Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений;
	
	Для каждого СтрТ Из СоответствиеОтвет.Получить("suggestions") Цикл
		
		СписокЗначений.Добавить(СтрТ, СтрТ.Получить("value"));
		
	КонецЦикла;
	
	ДанныеВыбора = СписокЗначений;
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&Вместо("ОбработкаВыбора")
Процедура dd_ОбработкаВыбора(Знач Форма, Знач ВыбранноеЗначение, Знач ИмяРеквизита, СтандартнаяОбработка = Ложь) Экспорт
	
	НастройкиПодсистемы = dd_ОбщийМодульПовторноеИспользование.ПолучитьНастройкиПодсистемы();
	
	Если НастройкиПодсистемы.ИспользованиеСервиса Тогда
		
		ДанныеАдреса = dd_ЗапросыКСервису.СформироватьКИФормата1С(ВыбранноеЗначение);
		Представление = ВыбранноеЗначение.Получить("Value");
		
		ВыбранноеЗначение = Новый Структура;
		
		ВыбранноеЗначение.Вставить("Адрес", ДанныеАдреса.КонтактнаяИнформация);
		ВыбранноеЗначение.Вставить("Представление", ДанныеАдреса.Представление);
		
	КонецЕсли;
	
	ПродолжитьВызов(Форма, ВыбранноеЗначение, ИмяРеквизита, СтандартнаяОбработка);
	
	Если НастройкиПодсистемы.ИспользованиеСервиса Тогда
		
		Форма.ОбновитьОтображениеДанных(Форма.Элементы[ИмяРеквизита]);
		Позиция = СтрДлина(Форма.Элементы[ИмяРеквизита].ТекстРедактирования) + 1;
		Форма.Элементы[ИмяРеквизита].УстановитьГраницыВыделения(Позиция, Позиция);
		
	КонецЕсли;
	
КонецПроцедуры
