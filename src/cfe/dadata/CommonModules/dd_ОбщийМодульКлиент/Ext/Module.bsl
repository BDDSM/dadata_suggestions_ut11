Процедура АвтоПодбор_СтрокаФИО(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка, Форма, Объект, Параметры) Экспорт
	
	Если Параметры.НаименованиеИзменено
		ИЛИ 
		СтрДлина(Текст) < 3 
		ИЛИ Прав(Текст, 1) = " " Тогда
		
		Параметры.НаименованиеИзменено = Ложь;
		Возврат;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	СоответствиеОтвет = dd_ЗапросыКСервису.ПолучитьСоответствиеПодсказок(Текст, "ФИО");
	
	Если СоответствиеОтвет.Количество() = 0  Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений;
	
	Для каждого СтрТ Из СоответствиеОтвет.Получить("suggestions") Цикл
		
		СписокЗначений.Добавить(СтрТ.Получить("value"));
		
	КонецЦикла;
	
	ДанныеВыбора = СписокЗначений;
	
КонецПроцедуры

Процедура ОбработкаВыбора_СтрокаФИО(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, Форма, Объект, Параметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение = Элемент.ТекстРедактирования  Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Наименование = ВыбранноеЗначение;
	Форма.ОбновитьОтображениеДанных(Форма.Элементы.Наименование);
	
	Позиция = СтрДлина(Элемент.ТекстРедактирования) + 1;
	Элемент.УстановитьГраницыВыделения(Позиция, Позиция);
	
	Форма.Модифицированность = Истина;
	
	Параметры.НаименованиеИзменено = Истина;
	
КонецПроцедуры

Процедура АвтоПодбор_СтрокаИНН(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	Если СтрДлина(Текст) < 10 Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	СоответствиеОтвет = dd_ЗапросыКСервису.ПолучитьСоответствиеПодсказок(Текст, "ОрганизацияИНН");
	
	Если СоответствиеОтвет.Количество() = 0  Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений;
	
	Для каждого СтрТ Из СоответствиеОтвет.Получить("suggestions") Цикл
		
		СписокЗначений.Добавить(СтрТ.Получить("data"), СтрТ.Получить("value"));
		
	КонецЦикла;
	
	ДанныеВыбора = СписокЗначений;
	
КонецПроцедуры

Функция ПолучитьРеквизитыКонтрагента(ВыбранноеЗначение) Экспорт
	
	РеквизитыКонтрагента = Новый Структура;
	РеквизитыКонтрагента.Вставить("ИНН", "");
	РеквизитыКонтрагента.Вставить("КПП", "");
	РеквизитыКонтрагента.Вставить("Наименование", "");
	РеквизитыКонтрагента.Вставить("НаименованиеПолное", "");
	РеквизитыКонтрагента.Вставить("НаименованиеСокращенное", "");
	
	СтруктураЮридическийАдрес = Новый Структура;
	СтруктураЮридическийАдрес = Новый Структура;
	СтруктураЮридическийАдрес.Вставить("Комментарий", "");
	СтруктураЮридическийАдрес.Вставить("КонтактнаяИнформация", "");
	СтруктураЮридическийАдрес.Вставить("Корректный", Неопределено);
	СтруктураЮридическийАдрес.Вставить("Представление", "");

	РеквизитыКонтрагента.Вставить("ЮридическийАдрес", СтруктураЮридическийАдрес);
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат РеквизитыКонтрагента;
	КонецЕсли;
	
	РеквизитыКонтрагента.ИНН = ВыбранноеЗначение.Получить("inn");
	РеквизитыКонтрагента.КПП = ВыбранноеЗначение.Получить("kpp");
	
	РеквизитыКонтрагента.Наименование = ВыбранноеЗначение.Получить("name").Получить("short_with_opf");
	РеквизитыКонтрагента.НаименованиеПолное = ВыбранноеЗначение.Получить("name").Получить("full_with_opf");
	РеквизитыКонтрагента.НаименованиеСокращенное = РеквизитыКонтрагента.Наименование; 
	
	РеквизитыКонтрагента.ЮридическийАдрес = dd_ЗапросыКСервису.СформироватьКИФормата1С(ВыбранноеЗначение.Получить("address"));

	Возврат РеквизитыКонтрагента;
	
КонецФункции

